/**
 * Holding an Issuer provides the ability to create amounts and empty purses,
 * but confers no rights. It is also the mechanism used to get exclusive access
 * to a Purse or Payment that you already hold, or to burn some or all of the
 * contained rights.
 */
interface Issuer (Amount (Quantity)) {
  /**
   * Get the label for this Issuer.
   *
   * Labels can be used to manually construct amounts
   */
  getLabel() -> (Comparable);

  /* Get the Assay for this Issuer. */
  getAssay() -> (Assay);

  /* Get the Strategy for this Issuer. */
  getStrategy() -> (Strategy);

  /** Make an Amount that contains the indicated quantity. */
  makeAmount(quantity :Quantity) -> (Amount);

  /** Make an empty purse associated with this kind of right. */
  makeEmptyPurse(name :String) -> (Purse);

  /** Combine multiple payments into one payment. */
  combine(paymentsArray :List(Payment), name :String = 'combined payment')
    -> (Payment);

  /** 
   * Split a single payment into multiple payments, according to the
   * amounts and names passed in. 
   */
  split(payment :Payment, amountsArray :List(Amount), namesArray :List(String))
    -> (List(Payment));

  /**
   * Make a new Payment that has exclusive rights to all the contents
   * of src. If amount does not equal the balance of the src payment,
   * throws error.
   */
  claimExactly(amount :Amount, src :Payment, name :String = 'a payment')
    -> (Payment);

  /**
   * Make a new Payment that has exclusive rights to all the contents of src.
   */
  claimAll(src :Payment, name :String = 'a payment') -> (Payment);

  /** 
   * Burn all of the rights from src. If amount does not equal the
   * balance of the src payment, throw error. 
   */
  burnExactly(amount :Amount, src :Payment) -> (Amount);

  /** Burn all of the rights from src. */
  burnAll(src :Payment) -> (Amount);
}

/**
 * Makes Mints.
 *
 * The description becomes part of the label, used by the assay to identify
 * amounts authorized/acknowledged by the mint. The MintController and Assay
 * must be compatible with the type of asset managed by the mint.
 *
 * Description doesn't have to be a string, but it will only be used for its
 * value.
 */
interface MintMaker {
  makeMint(
      description :Comparable,
      makeConfig :MintConfigMaker) -> (Mint);
}

/**
 * Holding a Mint carries the right to control issuance and destruction of
 * purses and payments containing amounts of a particular currency. Purses (and
 * payments) associated with a particular issuer can only transfer value to
 * others using the same mint.
 */
interface Mint (Amount (Quantity)) {
  /** Get the Issuer for this mint. */
  getIssuer() -> (Issuer);

  /**
   * Create a new Purse containing the amount. Give it the specified name or 'fa
   * purse'.
   */
  mint(initialBalance :Amount, name :String = 'a purse') -> (Purse);
}

/**
 * Purses hold verified amounts of certain rights issued by Mints. Purses can
 * transfer part of the balance they hold in a payment, which has a narrower
 * interface. A purse's balance can rise and fall, through the action of
 * depositExactly() and withdraw(). Operations on payments
 * (burnExactly(), depositExactly(), issuer.claimExactly()) kill the
 * original payment and create new payments if applicable.
 *
 * The primary use for Purses and Payments is for currency-like and goods-like
 * valuables, but they can also be used to represent other kinds of rights, such
 * as the right to participate in a particular contract.
 */
interface Purse (Amount) {
  /** Get the name of this purse. */
  getName() -> (String);

  /** Get the Issuer for this mint. */
  getIssuer() -> (Issuer);

  /** Get the amount contained in this purse, confirmed by the issuer. */
  getBalance() -> (Amount);

  /**
   * Deposit all the contents of srcPayment into this purse, returning the
   * amount. If the amount does not equal the balance of srcPayment,
   * throw error.
   */
  depositExactly(amount :Amount, srcPayment :Payment) -> (Amount);

  /**
   * Deposit all the contents of srcPayment into this purse, returning the
   * amount.
   */
  depositAll(srcPayment :Payment) -> (Amount);

  /** Withdraw amount from this purse into a new Payment. */
  withdraw(amount :Amount, name :String) -> (Payment);

  /** Withdraw entire content of this purse into a new Payment. */
  withdrawAll(name :String) -> (Payment);
}

/**
 * Payments hold verified amounts of certain rights issued by Mints. Amounts
 * from payments can be deposited in purses, but otherwise, the entire amount is
 * available when the payment is transferred. A payment's balance can only fall,
 * through the action of depositExactly(), claimExactly() or burnExactly().
 *
 * Payments can be converted to Purses by getting a verified issuer and calling
 * issuer.makeEmptyPurse().depositAll(payment);
 */
interface Payment (Amount) {
  /** Get the name of this purse. */
  getName() -> (String);

  /** Get the Issuer for this mint. */
  getIssuer() -> (Issuer);

  /** Get the amount contained in this payment, confirmed by the issuer. */
  getBalance() -> (Amount);
}

/** 
 * All of the difference in how an assay behaves can be reduced to
 * the behavior of the set operations on quantities (think: arithmetic)
 * such as `empty`, `with`, `without`, `includes`, etc. We extract this
 * custom logic into a strategy. Strategies are about quantity
 * arithmetic, whereas Assays are about Amounts, which are labeled
 * quantities. Assays use Strategies to do their quantity arithmetic,
 * and then label the results, making new Amounts. 
 */ 
interface Strategy () {
  /** 
   * Check the kind of this quantity and throw if it is not the
   * expected kind. 
   */
  insistKind(allegedQuantity :Quantity) -> (Quantity);

  /** Get the representation for empty */
  empty() -> (Quantity);

  /** Is the quantity empty? */
  isEmpty(quantity :Quantity) -> (boolean);

  /** Does the whole include the part? */
  includes(whole :Quantity, part :Quantity) -> (boolean);

  /** Does left equal right? */
  equals(left :Quantity, right :Quantity) -> (boolean);

  /** Return the left combined with the right */
  with(left :Quantity, right :Quantity) -> (Quantity);

  /** Return what remains after removing the part from the whole */
  without(whole :Quantity, part :Quantity) -> (Quantity);
}
